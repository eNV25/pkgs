# Maintainer: eNV25 <env252525@gmail.com>

pkgname=void-tools
_pkgname=void-runit
pkgver=20250212
pkgrel=1
pkgdesc="Some not Void specific tools from void-runit"
arch=(x86_64)
url="https://github.com/void-linux/$_pkgname"
license=('custom: public domain' 'custom: CC0')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")

build() {
	cd "$_pkgname-$pkgver"
	make
}

package() {
	cd "$_pkgname-$pkgver"
	make DESTDIR="$pkgdir" PREFIX=/usr install
	cd "${pkgdir}"
	rm -vrf etc/
	rm -vrf usr/lib
	mv -v usr/sbin usr/bin
	for _cmd in halt poweroff reboot shutdown vlogger; do
		rm -vf usr/bin/$_cmd usr/share/man/man*/$_cmd.*
	done
}

sha256sums=('c0d3ecea4dae5bba8ca3e563706847ef3b6cbdfcc19b8bf69b3f7a9bc0451f50')
